0 ' 28x12 = 56x24 pixels
5 CLEAR 1000
10 DIM R$(11),R(11):D$="?"+CHR$(94)+CHR$(10)+CHR$(8)+CHR$(9)
20 FOR C=0 TO 6:R$=R$+STRING$(4,143+16*C):NEXT
30 INPUT "GAME #";G
40 R=RND(-G)
50 FOR R=0 TO 11:R$(R)=R$:R(R)=RND(28)-1:NEXT
60 CLS 0
70 ' DRAW SCREEN
80 FOR X=3 TO 60:SET(X,2,4):SET(63-X,29,4):NEXT
90 FOR Y=3 TO 28:SET(2,31-Y,4):SET(61,Y,4):NEXT
100 PRINT@3,"GAME:      ";:PRINT@8,G;:PRINT@18,"MOVE:      ";
110 FOR R=0 TO 11:GOSUB 410:NEXT
120 ' START GAME
130 SCREEN 0,1:R=5
140 PRINT@23,M;
150 PRINT@64+R*32,"<";:PRINT@95+R*32,">";:SCREEN 0,1
160 K$=INKEY$:IF K$="" THEN 160
170 PRINT@64+R*32,CHR$(128);:PRINT@95+R*32,CHR$(128);
180 LN=INSTR(D$,K$)
190 ON LN GOTO 150,210,240,270,300
200 ' UP
210 IF R>0 THEN R=R-1
220 GOTO 150
230 ' DOWN
240 IF R<11 THEN R=R+1
250 GOTO 150
260 ' LEFT
270 R(R)=R(R)+1:IF R(R)>27 THEN R(R)=0
280 GOTO 320
290 ' RIGHT
300 R(R)=R(R)-1:IF R(R)<0 THEN R(R)=27
310 ' INC MOVE AND DISPLAY
320 M=M+1:GOSUB 410
330 ' CHECK FOR WIN
340 FOR I=11 TO 1 STEP-1:IF R(I)<>R(I-1) THEN 390
350 NEXT
360 ' ALL MATCH. WINNER!
370 FOR I=1 TO 10:SCREEN 0,I AND 1:FOR J=1 TO 100:NEXT:NEXT
375 GOTO 370
380 ' ALL DID NOT MATCH
390 GOTO 140
400 ' DISPLAY ROW
410 PRINT@66+R*32,MID$(R$(R),R(R)+1);:IF R(R)>0 THEN PRINT LEFT$(R$(R),R(R));
420 RETURN

