10 'RANDOMIZE
20 A=RND(-TIMER)
30 DIM A(10,10),X(25),Y(25)
40 FOR X=2 TO 8 STEP 2:FOR Y=1 TO 9 STEP 2:A(X,Y)=1:NEXT Y:NEXT X
50 FOR X=1 TO 9 STEP 2:FOR Y=2 TO 8 STEP 2:A(X,Y)=1:NEXT Y:NEXT X
60 N=24:S=0:X(0)=1:Y(0)=1:X=1:Y=1:A(X,Y)=1
70 GOSUB 160:A(X1,Y1)=0:A(X2,Y2)=1:N=N-1:X=X2:Y=Y2:S=S+1:X(S)=X:Y(S)=Y
80 IF N>0 THEN 70
90 GOSUB 210:STOP
100 D=INT(4*RND(0)+1)
110 ON D GOTO 120,130,140,150
120 X1=X:Y1=Y-1:X2=X:Y2=Y-2:RETURN
130 X1=X:Y1=Y+1:X2=X:Y2=Y+2:RETURN
140 X1=X-1:Y1=Y:X2=X-2:Y2=Y:RETURN
150 X1=X+1:Y1=Y:X2=X+2:Y2=Y:RETURN
160 GOSUB 360:IF F>0 THEN 180
170 S=S-1:X=X(S):Y=Y(S):GOTO 160
180 GOSUB 100:IF A(X1,Y1)=0 THEN 180
190 IF A(X2,Y2)=1 THEN 180
200 RETURN
210 PRINT "+---+---+---+---+---+"
220 FOR I=1 TO 9 STEP 2:PRINT "!";:FOR J=2 TO 8 STEP 2
230 IF A(J,I)=0 THEN 250
240 PRINT "   !";:GOTO 260
250 PRINT "    ";
260 NEXT J
270 PRINT "   !":IF I=9 THEN 330
280 PRINT "+";:FOR J=1 TO 9 STEP 2
290 IF A(J,I+1) > 0 THEN 310
300 PRINT "   +";:GOTO 320
310 PRINT "---+";
320 NEXT J:PRINT
330 NEXT I
340 PRINT "+---+---+---+---+---+"
350 RETURN
360 F=0:IF A(X-1,Y)=0 THEN 390
370 IF A(X-2,Y)=1 THEN 390
380 F=1
390 IF A(X+1,Y)=0 THEN 420
400 IF A(X+2,Y)=1 THEN 420
410 F=1
420 IF A(X,Y-1)=0 THEN 450
430 IF A(X,Y-2)=1 THEN 450
440 F=1
450 IF A(X,Y+1)=0 THEN 480
460 IF A(X,Y+2)=1 THEN 480
470 F=1
480 RETURN
490 END
