10 HSCREEN 2:PALETTE 0,0:PALETTE 1,21:HCOLOR 1,0
20 HBUFF 1,2560:HCLS:HGET (0,0)-(320,8),1
30 FOR X = 0 TO 15:READ A:READ V:POKE A,V:NEXT
40 REM VARIABLES TO HOLD THE START POSITION AND LENGTH FOR EACH SCREEN LINE
50 DIM ST(24),LE(24):LE(0)=0:ST(0)=10:W=10
60 REM SCREEN FILL
70 FORX=1TO23:ST(X)=RND(20):LE(X)=RND(3)-2:NEXT
80 L$="":FOR X = 1 TO 60:L$=L$+CHR$(47+45*(RND(2)-1)):NEXT
90 FORY=0TO23:HPRINT(0,Y),MID$(L$,ST(Y), LE(Y)+W)+STRING$(W," ")+MID$(L$,ST(Y), 40-ST(Y)-LE(Y)+W):NEXT
100 REM END SCREEN FILL
110 FORZ=1TO1000
120 REM ADVANCE LINES & REDRAW
130 FOR X=23 TO 1 STEP -1:ST(X)=ST(X-1):LE(X)=LE(X-1):NEXT
140 ST(0)=RND(20):LE(0)=RND(3)-2
150 FORY=0TO184 STEP 8:LN=Y/8:HPUT (0,Y)-(320,Y+8),1,PSET:HPRINT(0,LN),MID$(L$,ST(LN), LE(LN)+W)+STRING$(W," ")+MID$(L$,ST(LN), 40-ST(LN)-LE(LN)+W):NEXT
160 NEXT
170 GOTO 170
180 DATA &HF115,1,&HF116,2,&HF117,4,&HF118,8,&HF119,16,&HF11A,32,&HF11B,64,&HF11C,128,&HF27D,128,&HF27E,64,&HF27F,32,&HF280,16,&HF281,8,&HF282,4,&HF283,2,&HF284,1
