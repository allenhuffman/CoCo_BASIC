10 CLEAR 500
20 ' MAX OUTPUT LINE LENGTH
30 INPUT "MAX LINE LENGTH";MX
40 IF MX=0 OR MX>250 THEN MX=250
50 ' GET/SET DEVICE NUMBER
60 DV=0
70 PRINT "OUTPUT TO D)ISK, P)RINTER"
80 INPUT "S)CREEN OR T)APE";A$
90 IF A$="D" THEN DV=1 ELSE IF A$="P" THEN DV=-2 ELSE IF A$="S" THEN DV=0 ELSE IF A$="T" THEN DV=-1 ELSE 70
100 ' CREATE OUTPUT FILE
110 OPEN "O",#DV,"MAZEDATA.BAS"
120 ' LINE NUMBER/RESET STRING
130 LN=1:LN$=""
140 ' READ LINE OF MAZE DATA
150 READ A$:IF A$="" THEN 470
160 PRINT A$
170 ' START AT FIRST CHARACTER
180 CH=1
190 ' INIT BIT VALUE
200 BT=128
210 ' RESET OUTPUT VALUE
220 V=0
230 ' IF X, ADD 1 (SET BIT)
240 IF MID$(A$,CH,1)="X" THEN V=V+BT
250 PRINT MID$(A$,CH,1);
260 ' SHIFT BIT LEFT
270 BT=INT(BT/2)
280 ' IF MORE BITS, DO NEXT CH
290 IF BT>0 THEN CH=CH+1:GOTO 240
300 ' CREATE 2-DIGIT HEX VALUE
310 HX$=HEX$(V):IF V<16 THEN HX$="0"+HX$
320 PRINT ,HX$;CH
330 ' START LINE IF NEEDED
340 ' TRIM FIRST CHAR
350 IF LN$="" THEN LN$=STR$(LN)+"DATA":LN$=MID$(LN$,2)
360 ' ADD HEX VALUE
370 LN$=LN$+HX$
380 'IF LINE FULL, WRITE IT OUT
390 IF LEN(LN$)>=MX-2 THEN PRINT #DV,LN$:LN$="":LN=LN+1
400 ' GO TO NEXT CH
410 CH=CH+1
420 ' IF PAST END, NEW LINE
430 IF CH>LEN(A$) THEN 150
440 ' OTHERWISE, NEXT BYTE
450 GOTO 200
460 ' WRITE ANY REMAINING DATA
470 PRINT #DV,LN$
480 CLOSE #DV
490 END
500 DATA "XXXXXXXXXXXXXXXXXXXXXXXXXXXX"
510 DATA "X            XX            X"
520 DATA "X XXXX XXXXX XX XXXXX XXXX X"
530 DATA "X XXXX XXXXX XX XXXXX XXXX X"
540 DATA "X XXXX XXXXX XX XXXXX XXXX X"
550 DATA "X                          X"
560 DATA "X XXXX XX XXXXXXXX XX XXXX X"
570 DATA "X XXXX XX XXXXXXXX XX XXXX X"
580 DATA "X      XX    XX    XX      X"
590 DATA "XXXXXX XXXXX XX XXXXX XXXXXX"
600 DATA "     X XXXXX XX XXXXX X     "
610 DATA "     X XX          XX X     "
620 DATA "     X XX XXXXXXXX XX X     "
630 DATA "XXXXXX XX X      X XX XXXXXX"
640 DATA "          X      X          "
650 DATA "XXXXXX XX X      X XX XXXXXX"
660 DATA "     X XX XXXXXXXX XX X     "
670 DATA "     X XX          XX X     "
680 DATA "     X XX XXXXXXXX XX X     "
690 DATA "XXXXXX XX XXXXXXXX XX XXXXXX"
700 DATA "X            XX            X"
710 DATA "X XXXX XXXXX XX XXXXX XXXX X"
720 DATA "X XXXX XXXXX XX XXXXX XXXX X"
730 DATA "X   XX                XX   X"
740 DATA "XXX XX XX XXXXXXXX XX XX XXX"
750 DATA "XXX XX XX XXXXXXXX XX XX XXX"
760 DATA "X      XX    XX    XX      X"
770 DATA "X XXXXXXXXXX XX XXXXXXXXXX X"
780 DATA "X XXXXXXXXXX XX XXXXXXXXXX X"
790 DATA "X                          X"
800 DATA "XXXXXXXXXXXXXXXXXXXXXXXXXXXX"
810 DATA ""
820 DATA ""

