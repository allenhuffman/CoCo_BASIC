0 CLS:FORI=0TO8:READI$:POKE1024+I,VAL("&H"+I$):NEXTI:EXEC1024:DATAC6,1,96,BC,1F,2,7E,96,A3:CLEAR21000:DIMB$(200),A$(19,10),I$,B$,A$,F$,G$,D$,H$,C$,E$,I,G,K,C,D,E,J,F,H,A:D$=CHR$(12)+CHR$(14):G$="*==============*==============*":GOSUB62
1 CLS:PRINTTAB(6)"*ALL RAM* BBS SYSTEM":PRINT"USERS:"F,"CALLS:"C:PRINTTAB(5)"SYSTEM AWAITING CALLER";:GOSUB64:SOUND200,10:I$="Welcome To *ALL RAM* BBS!":GOSUB68:H=0:C=C+1
2 PRINT:PRINT"Password or 'NEW' :";:A=1:GOSUB64:C$=I$:IFI$=""ORI$="NEW"THEN6ELSEPRINT"Checking: ";:I=0
3 I$=B$(I):G=INSTR(I$,"\"):H$=LEFT$(I$,G-1):E$=MID$(I$,G+1,LEN(I$)-G-1):E=VAL(RIGHT$(I$,1)):IFE$=C$THEN5ELSEI=I+1:IFI<=F THEN3
4 PRINT"*INVALID*":H=H+1:IFH<3THEN2ELSE26
5 PRINT"*ACCEPTED*":PRINTG$:PRINT"On-Line: "H$:PRINT"Access :"E:PRINT"Caller :"C:H=0:GOTO12
6 I$="Password Application Form":GOSUB68:IFF=200THENPRINT"Sorry, the userlog is full now.":GOTO26ELSEPRINT"Name=20 chars, Password=8 chars"
7 PRINT:PRINT"Full Name :";:A=1:GOSUB64:H$=I$:IFI$=""ORLEN(I$)>20THEN2
8 PRINT"Password  :";:A=1:GOSUB64:E$=I$:IFI$=""ORLEN(I$)>8THEN2
9 PRINT:PRINT"Name :"H$:PRINT"Pswd :"E$:PRINT"Is this correct? ";:A=1:GOSUB64:IFLEFT$(I$,1)="Y"THEN10ELSE7
10 F=F+1:B$(F)=H$+"\"+E$+"0":E=0:H=0:PRINT"Your password will be validated as soon as time permits.  Press":PRINT"[ENTER] to continue :";:GOSUB64
11 I$="*ALL RAM* BBS Master Menu":GOSUB68:PRINT"C-all Sysop","P-ost Msg":PRINT"G-oodbye","R-ead Msg":PRINT"U-serlog","S-can Titles"
12 PRINTG$
13 H=H+1:IFH>200THENPRINT"Sorry, your time on-line is up.":GOTO25ELSEIFH>180THENPRINT"Please complete your call soon."
14 PRINTTAB(7)"?=Menu/Command :";:A=1:GOSUB64:I$=LEFT$(I$,1)
15 D=INSTR("?CGRSPU%",I$):IFD=0THENPRINT"*Invalid Command*":GOTO13
16 IFE<1ANDD>5THENPRINT" Sorry, you are not validated.":GOTO14
17 OND GOTO11,18,24,47,54,31,27,58
18 I$="Calling the Sysop":GOSUB68:I=0
19 PRINT" BEEP!";:SOUND150,5:IFINKEY$=CHR$(12)THEN21ELSEPRINTSTRING$(5,8);:I=I+1:IFI<25THEN19
20 PRINT:PRINT" The Sysop is unavaliable now.":GOTO12
21 PRINT:PRINTTAB(6)"*Chat mode engaged*"
22 GOSUB64:IFLEFT$(I$,3)="BYE"THEN23ELSE22
23 PRINTTAB(5)"*Chat mode terminated*":GOTO12
24 I$="Thank you for calling":GOSUB68
25 PRINT:PRINT"Goodbye, "H$"!":PRINT:PRINT"Please call again."
26 PRINT:PRINT:PRINT"*ALL RAM* BBS disconnecting...":FORI=1TO1000:NEXTI:GOTO1
27 I$="List of Users":GOSUB68:PRINT"Users on system:"F:IFF=0THEN12ELSEI=1
28 I$=B$(I):PRINTLEFT$(I$,INSTR(I$,"\")-1)TAB(29)RIGHT$(I$,1):IF(I/10)=INT(I/10)THENPRINT"C-ontinue or S-top :";:A=1:GOSUB64:IFLEFT$(I$,1)="S"THEN30
29 I=I+1:IFI<=F THEN28
30 PRINT"*End of Userlog*":GOTO12
31 IFJ=20THENPRINT"One moment, making room...":FORI=0TO18:FORG=0TO10:A$(I,G)=A$(I+1,G):NEXTG:NEXTI:J=19
32 CLS:PRINTD$"This will be message #"J+1:FORI=0TO10:A$(J,I)="":NEXTI:B$=H$:PRINT"From :"B$:PRINT"To   :";:A=1:GOSUB64:I$=LEFT$(I$,20):F$=I$:IFI$=""THEN12
33 PRINT"Is this message private? ";:A=1:GOSUB64:IFLEFT$(I$,1)="Y"THENA$="*E-Mail*":GOTO35
34 PRINT"Subj :";:A=1:GOSUB64:I$=LEFT$(I$,18):A$=I$:IFI$=""THEN12
35 PRINT"Enter up to 10 lines, 64 chars. [ENTER] on a blank line to end.":I=0
36 I=I+1:PRINTUSING"##>";I;:GOSUB64:A$(J,I)=I$:IFI$=""THENI=I-1:GOTO38ELSEIFI<10THEN36
37 PRINT"*Message Buffer Full*"
38 PRINT"A-bort, C-ont, E-dit, L-ist, or S-ave Message? ";:A=1:GOSUB64:I$=LEFT$(I$,1):IFI$=""THEN38
39 D=INSTR("ACELS",I$):IFD=0THEN38ELSEOND GOTO46,40,41,44,45
40 IFI<10THENPRINT"Continue your message:":GOTO36ELSE37
41 PRINT"Edit line 1 -"I":";:GOSUB64:D=VAL(LEFT$(I$,2)):IFD<1ORD>I THEN38
42 PRINT"Line currently reads:":PRINTA$(J,D):PRINT"Enter new line:":GOSUB64:I$=LEFT$(I$,64):IFI$=""THENPRINT"*Unchanged*"ELSEA$(J,D)=I$:PRINT"*Corrected*"
43 GOTO41
44 CLS:PRINTD$"Message Reads:":FORG=1TOI:PRINTUSING"##>";G;:PRINTA$(J,G):NEXTG:GOTO38
45 A$(J,0)=F$+"\"+B$+"\"+A$:J=J+1:PRINT"*Message"J"stored*":GOTO12
46 PRINT"*Message Aborted*":GOTO12
47 IFJ=0THENPRINT"The message base is empty.":GOTO12
48 CLS:PRINTD$
49 PRINT"Read Message 1 -"J":";:GOSUB64:I=VAL(LEFT$(I$,2)):IFI<1ORI>J THEN12
50 I$=A$(I-1,0):G=INSTR(I$,"\"):K=INSTR(G+1,I$,"\"):F$=LEFT$(I$,G-1):B$=MID$(I$,G+1,K-G-1):A$=RIGHT$(I$,LEN(I$)-K):IFA$="*E-Mail*"ANDE<8THENIFH$<>F$ANDH$<>B$THENPRINT"That message is private.":GOTO49
51 CLS:PRINTD$"Message #"I:PRINT"From :"B$:PRINT"To   :"F$:PRINT"Subj :"A$:PRINT:G=0
52 G=G+1:PRINTA$(I-1,G):IFA$(I-1,G)=""THEN53ELSEIFG<10THEN52
53 PRINT"*End of Message*":GOTO49
54 IFJ=0THENPRINT"The message base is empty.":GOTO12
55 CLS:PRINTD$"Message Titles:":I=0
56 I$=A$(I,0):PRINTUSING"[##] SB: ";I+1;:G=INSTR(I$,"\"):K=INSTR(G+1,I$,"\"):PRINTRIGHT$(I$,LEN(I$)-K):PRINTTAB(5)"TO: "LEFT$(I$,G-1):I=I+1:IFI<J THEN56
57 PRINT"*End of Messages*":GOTO12
58 IFE<9THENI$="Z":GOTO15
59 PRINT"PASSWORD?";:GOSUB64:IFI$<>"?DROWSSAP"THENPRINT"Thank You!":GOTO12
60 PRINT"Abort BBS? YES or NO? ";:A=1:GOSUB64:IFI$<>"YES"THEN12
61 GOSUB63:STOP
62 PRINT"%LOAD% [ENTER] WHEN READY";:GOSUB64:OPEN"I",#-1,"USERLOG":INPUT#-1,C,F:FORI=0TOF:INPUT#-1,B$(I):NEXTI:CLOSE:OPEN"I",#-1,"MSG BASE":INPUT#-1,J:FORI=0TOJ-1:FORG=0TO10:INPUT#-1,A$(I,G):NEXTG:NEXTI:CLOSE:RETURN
63 PRINT"%SAVE% [ENTER] WHEN READY";:GOSUB64:MOTORON:FORI=0TO999:NEXTI:OPEN"O",#-1,"USERLOG":PRINT#-1,C,F:FORI=0TOF:PRINT#-1,B$(I):NEXTI:CLOSE:OPEN"O",#-1,"MSG BASE":PRINT#-1,J:FORI=0TOJ-1:FORG=0TO10:PRINT#-1,A$(I,G):NEXTG:NEXTI:CLOSE:RETURN
64 LINEINPUTI$:I$=LEFT$(I$,64):IFA=0ORI$=""THENRETURN
65 FORK=1TOLEN(I$):B=ASC(MID$(I$,K,1)):IFB>96THENMID$(I$,K,1)=CHR$(B-32)
66 IFB=92THENMID$(I$,K,1)="/"
67 NEXTK:A=0:RETURN
68 CLS:PRINTD$G$:PRINTTAB((32-LEN(I$))/2)I$:PRINTG$:RETURN
