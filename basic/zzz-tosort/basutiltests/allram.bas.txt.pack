0 CLS:FORB=0TO8:READB$
1 POKE1024+B,VAL("&H"+B$):NEXTB
2 EXEC1024
3 DATA C6,1,96,BC,1F,2,7E,96,A3
4 CLEAR21000
5 DIMA$(200),B$(19,10),B$,I$,G$,C$,H$,A$,F$,E$,D$,B,H,I,A,C,J,G,F,K,D
6 A$=CHR$(12)+CHR$(14)
7 H$="*==============*==============*"
8 GOSUB169
9 CLS
10 PRINTTAB(6)"*ALL RAM* BBS SYSTEM"
11 PRINT"USERS:"F,"CALLS:"A
12 PRINTTAB(5)"SYSTEM AWAITING CALLER";
13 GOSUB186:SOUND200,10
14 B$="Welcome To *ALL RAM* BBS!"
15 GOSUB193:K=0:A=A+1
16 PRINT:PRINT"Password or 'NEW' :";
17 D=1:GOSUB186:E$=B$
18 IFB$=""ORB$="NEW"THEN30ELSEPRINT"Checking: ";:B=0
19 B$=A$(B):H=INSTR(B$,"\")
20 F$=LEFT$(B$,H-1)
21 D$=MID$(B$,H+1,LEN(B$)-H-1)
22 J=VAL(RIGHT$(B$,1))
23 IFD$=E$THEN26ELSEB=B+1:IFB<=F THEN19
24 PRINT"*INVALID*":K=K+1
25 IFK<3THEN16ELSE80
26 PRINT"*ACCEPTED*":PRINTH$
27 PRINT"On-Line: "F$
28 PRINT"Access :"J:PRINT"Caller :"A
29 K=0:GOTO54
30 B$="Password Application Form"
31 GOSUB193
32 IFF=200THENPRINT"Sorry, the userlog is full now.":GOTO80ELSEPRINT"Name=20 chars, Password=8 chars"
33 PRINT:PRINT"Full Name :";:D=1
34 GOSUB186:F$=B$
35 IFB$=""ORLEN(B$)>20THEN16
36 PRINT"Password  :";:D=1
37 GOSUB186:D$=B$
38 IFB$=""ORLEN(B$)>8THEN16
39 PRINT:PRINT"Name :"F$
40 PRINT"Pswd :"D$
41 PRINT"Is this correct? ";:D=1
42 GOSUB186
43 IFLEFT$(B$,1)="Y"THEN44ELSE33
44 F=F+1:A$(F)=F$+"\"+D$+"0":J=0
45 K=0
46 PRINT"Your password will be validated as soon as time permits.  Press"
47 PRINT"[ENTER] to continue :";
48 GOSUB186
49 B$="*ALL RAM* BBS Master Menu"
50 GOSUB193
51 PRINT"C-all Sysop","P-ost Msg"
52 PRINT"G-oodbye","R-ead Msg"
53 PRINT"U-serlog","S-can Titles"
54 PRINTH$
55 K=K+1
56 IFK>200THENPRINT"Sorry, your time on-line is up.":GOTO78ELSEIFK>180THENPRINT"Please complete your call soon."
57 PRINTTAB(7)"?=Menu/Command :";
58 D=1:GOSUB186:B$=LEFT$(B$,1)
59 C=INSTR("?CGRSPU%",B$)
60 IFC=0THENPRINT"*Invalid Command*":GOTO55
61 IFJ<1ANDC>5THENPRINT" Sorry, you are not validated.":GOTO57
62 ONC GOTO49,63,76,133,150,91,83,162
63 B$="Calling the Sysop":GOSUB193
64 B=0
65 PRINT" BEEP!";:SOUND150,5
66 IFINKEY$=CHR$(12)THEN70ELSEPRINTSTRING$(5,8);:B=B+1:IFB<25THEN65
67 PRINT
68 PRINT" The Sysop is unavaliable now."
69 GOTO54
70 PRINT
71 PRINTTAB(6)"*Chat mode engaged*"
72 GOSUB186
73 IFLEFT$(B$,3)="BYE"THEN74ELSE72
74 PRINTTAB(5)"*Chat mode terminated*"
75 GOTO54
76 B$="Thank you for calling"
77 GOSUB193
78 PRINT:PRINT"Goodbye, "F$"!"
79 PRINT:PRINT"Please call again."
80 PRINT:PRINT
81 PRINT"*ALL RAM* BBS disconnecting..."
82 FORB=1TO1000:NEXTB:GOTO9
83 B$="List of Users":GOSUB193
84 PRINT"Users on system:"F
85 IFF=0THEN54ELSEB=1
86 B$=A$(B)
87 PRINTLEFT$(B$,INSTR(B$,"\")-1)TAB(29)RIGHT$(B$,1)
88 IF(B/10)=INT(B/10)THENPRINT"C-ontinue or S-top :";:D=1:GOSUB186:IFLEFT$(B$,1)="S"THEN90
89 B=B+1:IFB<=F THEN86
90 PRINT"*End of Userlog*":GOTO54
91 IFG=20THENPRINT"One moment, making room...":FORB=0TO18:FORH=0TO10:B$(B,H)=B$(B+1,H):NEXTH:NEXTB:G=19
92 CLS
93 PRINTA$"This will be message #"G+1
94 FORB=0TO10:B$(G,B)="":NEXTB
95 I$=F$:PRINT"From :"I$
96 PRINT"To   :";:D=1:GOSUB186
97 B$=LEFT$(B$,20):C$=B$
98 IFB$=""THEN54
99 PRINT"Is this message private? ";
100 D=1:GOSUB186
101 IFLEFT$(B$,1)="Y"THENG$="*E-Mail*":GOTO105
102 PRINT"Subj :";:D=1:GOSUB186
103 B$=LEFT$(B$,18):G$=B$
104 IFB$=""THEN54
105 PRINT"Enter up to 10 lines, 64 chars. [ENTER] on a blank line to end."
106 B=0
107 B=B+1:PRINTUSING"##>";B;
108 GOSUB186:B$(G,B)=B$
109 IFB$=""THENB=B-1:GOTO111ELSEIFB<10THEN107
110 PRINT"*Message Buffer Full*"
111 PRINT"A-bort, C-ont, E-dit, L-ist, or S-ave Message? ";
112 D=1:GOSUB186:B$=LEFT$(B$,1)
113 IFB$=""THEN111
114 C=INSTR("ACELS",B$)
115 IFC=0THEN111ELSEONC GOTO132,116,117,126,129
116 IFB<10THENPRINT"Continue your message:":GOTO107ELSE110
117 PRINT"Edit line 1 -"B":";
118 GOSUB186:C=VAL(LEFT$(B$,2))
119 IFC<1ORC>B THEN111
120 PRINT"Line currently reads:"
121 PRINTB$(G,C)
122 PRINT"Enter new line:":GOSUB186
123 B$=LEFT$(B$,64)
124 IFB$=""THENPRINT"*Unchanged*"ELSEB$(G,C)=B$:PRINT"*Corrected*"
125 GOTO117
126 CLS:PRINTA$"Message Reads:"
127 FORH=1TOB:PRINTUSING"##>";H;
128 PRINTB$(G,H):NEXTH:GOTO111
129 B$(G,0)=C$+"\"+I$+"\"+G$:G=G+1
130 PRINT"*Message"G"stored*"
131 GOTO54
132 PRINT"*Message Aborted*":GOTO54
133 IFG=0THENPRINT"The message base is empty.":GOTO54
134 CLS:PRINTA$
135 PRINT"Read Message 1 -"G":";
136 GOSUB186:B=VAL(LEFT$(B$,2))
137 IFB<1ORB>G THEN54
138 B$=B$(B-1,0):H=INSTR(B$,"\")
139 I=INSTR(H+1,B$,"\")
140 C$=LEFT$(B$,H-1)
141 I$=MID$(B$,H+1,I-H-1)
142 G$=RIGHT$(B$,LEN(B$)-I)
143 IFG$="*E-Mail*"ANDJ<8THENIFF$<>C$ANDF$<>I$THENPRINT"That message is private.":GOTO135
144 CLS:PRINTA$"Message #"B
145 PRINT"From :"I$:PRINT"To   :"C$
146 PRINT"Subj :"G$:PRINT:H=0
147 H=H+1:PRINTB$(B-1,H)
148 IFB$(B-1,H)=""THEN149ELSEIFH<10THEN147
149 PRINT"*End of Message*":GOTO135
150 IFG=0THENPRINT"The message base is empty.":GOTO54
151 CLS:PRINTA$"Message Titles:"
152 B=0
153 B$=B$(B,0)
154 PRINTUSING"[##] SB: ";B+1;
155 H=INSTR(B$,"\")
156 I=INSTR(H+1,B$,"\")
157 PRINTRIGHT$(B$,LEN(B$)-I)
158 PRINTTAB(5)"TO: "LEFT$(B$,H-1)
159 B=B+1:IFB<G THEN153
160 PRINT"*End of Messages*"
161 GOTO54
162 IFJ<9THENB$="Z":GOTO59
163 PRINT"PASSWORD?";:GOSUB186
164 IFB$<>"?DROWSSAP"THENPRINT"Thank You!":GOTO54
165 PRINT"Abort BBS? YES or NO? ";
166 D=1:GOSUB186
167 IFB$<>"YES"THEN54
168 GOSUB177:STOP
169 PRINT"%LOAD% [ENTER] WHEN READY";
170 GOSUB186:OPEN"I",#-1,"USERLOG"
171 INPUT#-1,A,F:FORB=0TOF
172 INPUT#-1,A$(B):NEXTB:CLOSE
173 OPEN"I",#-1,"MSG BASE"
174 INPUT#-1,G:FORB=0TOG-1
175 FORH=0TO10:INPUT#-1,B$(B,H)
176 NEXTH:NEXTB:CLOSE:RETURN
177 PRINT"%SAVE% [ENTER] WHEN READY";
178 GOSUB186:MOTORON:FORB=0TO999
179 NEXTB:OPEN"O",#-1,"USERLOG"
180 PRINT#-1,A,F:FORB=0TOF
181 PRINT#-1,A$(B):NEXTB:CLOSE
182 OPEN"O",#-1,"MSG BASE"
183 PRINT#-1,G:FORB=0TOG-1
184 FORH=0TO10:PRINT#-1,B$(B,H)
185 NEXTH:NEXTB:CLOSE:RETURN
186 LINEINPUTB$:B$=LEFT$(B$,64)
187 IFD=0ORB$=""THENRETURN
188 FORI=1TOLEN(B$)
189 E=ASC(MID$(B$,I,1))
190 IFE>96THENMID$(B$,I,1)=CHR$(E-32)
191 IFE=92THENMID$(B$,I,1)="/"
192 NEXTI:D=0:RETURN
193 CLS:PRINTA$H$
194 PRINTTAB((32-LEN(B$))/2)B$
195 PRINTH$:RETURN
