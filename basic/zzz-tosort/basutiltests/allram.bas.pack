0 CLS:FORK=0TO8:READH$:POKE1024+K,VAL("&H"+H$):NEXTK:EXEC1024:CLEAR21000:DIMA$(200),B$(19,10),H$,E$,C$,D$,A$,F$,G$,I$,B$,K,C,A,B,E,F,I,D,G,H:F$=CHR$(12)+CHR$(14):A$="*==============*==============*":GOSUB62:DATA C6,1,96,BC,1F,2,7E,96,A3
1 CLS:PRINTTAB(6)"*ALL RAM* BBS SYSTEM":PRINT"USERS:"D,"CALLS:"B:PRINTTAB(5)"SYSTEM AWAITING CALLER";:GOSUB64:SOUND200,10:H$="Welcome To *ALL RAM* BBS!":GOSUB68:G=0:B=B+1
2 PRINT:PRINT"Password or 'NEW' :";:H=1:GOSUB64:I$=H$:IFH$=""ORH$="NEW"THEN6ELSEPRINT"Checking: ";:K=0
3 H$=A$(K):C=INSTR(H$,"\"):G$=LEFT$(H$,C-1):B$=MID$(H$,C+1,LEN(H$)-C-1):F=VAL(RIGHT$(H$,1)):IFB$=I$THEN5ELSEK=K+1:IFK<=D THEN3
4 PRINT"*INVALID*":G=G+1:IFG<3THEN2ELSE26
5 PRINT"*ACCEPTED*":PRINTA$:PRINT"On-Line: "G$:PRINT"Access :"F:PRINT"Caller :"B:G=0:GOTO12
6 H$="Password Application Form":GOSUB68:IFD=200THENPRINT"Sorry, the userlog is full now.":GOTO26ELSEPRINT"Name=20 chars, Password=8 chars"
7 PRINT:PRINT"Full Name :";:H=1:GOSUB64:G$=H$:IFH$=""ORLEN(H$)>20THEN2
8 PRINT"Password  :";:H=1:GOSUB64:B$=H$:IFH$=""ORLEN(H$)>8THEN2
9 PRINT:PRINT"Name :"G$:PRINT"Pswd :"B$:PRINT"Is this correct? ";:H=1:GOSUB64:IFLEFT$(H$,1)="Y"THEN10ELSE7
10 D=D+1:A$(D)=G$+"\"+B$+"0":F=0:G=0:PRINT"Your password will be validated as soon as time permits.  Press":PRINT"[ENTER] to continue :";:GOSUB64
11 H$="*ALL RAM* BBS Master Menu":GOSUB68:PRINT"C-all Sysop","P-ost Msg":PRINT"G-oodbye","R-ead Msg":PRINT"U-serlog","S-can Titles"
12 PRINTA$
13 G=G+1:IFG>200THENPRINT"Sorry, your time on-line is up.":GOTO25ELSEIFG>180THENPRINT"Please complete your call soon."
14 PRINTTAB(7)"?=Menu/Command :";:H=1:GOSUB64:H$=LEFT$(H$,1)
15 E=INSTR("?CGRSPU%",H$):IFE=0THENPRINT"*Invalid Command*":GOTO13
16 IFF<1ANDE>5THENPRINT" Sorry, you are not validated.":GOTO14
17 ONE GOTO11,18,24,47,54,31,27,58
18 H$="Calling the Sysop":GOSUB68:K=0
19 PRINT" BEEP!";:SOUND150,5:IFINKEY$=CHR$(12)THEN21ELSEPRINTSTRING$(5,8);:K=K+1:IFK<25THEN19
20 PRINT:PRINT" The Sysop is unavaliable now.":GOTO12
21 PRINT:PRINTTAB(6)"*Chat mode engaged*"
22 GOSUB64:IFLEFT$(H$,3)="BYE"THEN23ELSE22
23 PRINTTAB(5)"*Chat mode terminated*":GOTO12
24 H$="Thank you for calling":GOSUB68
25 PRINT:PRINT"Goodbye, "G$"!":PRINT:PRINT"Please call again."
26 PRINT:PRINT:PRINT"*ALL RAM* BBS disconnecting...":FORK=1TO1000:NEXTK:GOTO1
27 H$="List of Users":GOSUB68:PRINT"Users on system:"D:IFD=0THEN12ELSEK=1
28 H$=A$(K):PRINTLEFT$(H$,INSTR(H$,"\")-1)TAB(29)RIGHT$(H$,1):IF(K/10)=INT(K/10)THENPRINT"C-ontinue or S-top :";:H=1:GOSUB64:IFLEFT$(H$,1)="S"THEN30
29 K=K+1:IFK<=D THEN28
30 PRINT"*End of Userlog*":GOTO12
31 IFI=20THENPRINT"One moment, making room...":FORK=0TO18:FORC=0TO10:B$(K,C)=B$(K+1,C):NEXTC:NEXTK:I=19
32 CLS:PRINTF$"This will be message #"I+1:FORK=0TO10:B$(I,K)="":NEXTK:E$=G$:PRINT"From :"E$:PRINT"To   :";:H=1:GOSUB64:H$=LEFT$(H$,20):D$=H$:IFH$=""THEN12
33 PRINT"Is this message private? ";:H=1:GOSUB64:IFLEFT$(H$,1)="Y"THENC$="*E-Mail*":GOTO35
34 PRINT"Subj :";:H=1:GOSUB64:H$=LEFT$(H$,18):C$=H$:IFH$=""THEN12
35 PRINT"Enter up to 10 lines, 64 chars. [ENTER] on a blank line to end.":K=0
36 K=K+1:PRINTUSING"##>";K;:GOSUB64:B$(I,K)=H$:IFH$=""THENK=K-1:GOTO38ELSEIFK<10THEN36
37 PRINT"*Message Buffer Full*"
38 PRINT"A-bort, C-ont, E-dit, L-ist, or S-ave Message? ";:H=1:GOSUB64:H$=LEFT$(H$,1):IFH$=""THEN38
39 E=INSTR("ACELS",H$):IFE=0THEN38ELSEONE GOTO46,40,41,44,45
40 IFK<10THENPRINT"Continue your message:":GOTO36ELSE37
41 PRINT"Edit line 1 -"K":";:GOSUB64:E=VAL(LEFT$(H$,2)):IFE<1ORE>K THEN38
42 PRINT"Line currently reads:":PRINTB$(I,E):PRINT"Enter new line:":GOSUB64:H$=LEFT$(H$,64):IFH$=""THENPRINT"*Unchanged*"ELSEB$(I,E)=H$:PRINT"*Corrected*"
43 GOTO41
44 CLS:PRINTF$"Message Reads:":FORC=1TOK:PRINTUSING"##>";C;:PRINTB$(I,C):NEXTC:GOTO38
45 B$(I,0)=D$+"\"+E$+"\"+C$:I=I+1:PRINT"*Message"I"stored*":GOTO12
46 PRINT"*Message Aborted*":GOTO12
47 IFI=0THENPRINT"The message base is empty.":GOTO12
48 CLS:PRINTF$
49 PRINT"Read Message 1 -"I":";:GOSUB64:K=VAL(LEFT$(H$,2)):IFK<1ORK>I THEN12
50 H$=B$(K-1,0):C=INSTR(H$,"\"):A=INSTR(C+1,H$,"\"):D$=LEFT$(H$,C-1):E$=MID$(H$,C+1,A-C-1):C$=RIGHT$(H$,LEN(H$)-A):IFC$="*E-Mail*"ANDF<8THENIFG$<>D$ANDG$<>E$THENPRINT"That message is private.":GOTO49
51 CLS:PRINTF$"Message #"K:PRINT"From :"E$:PRINT"To   :"D$:PRINT"Subj :"C$:PRINT:C=0
52 C=C+1:PRINTB$(K-1,C):IFB$(K-1,C)=""THEN53ELSEIFC<10THEN52
53 PRINT"*End of Message*":GOTO49
54 IFI=0THENPRINT"The message base is empty.":GOTO12
55 CLS:PRINTF$"Message Titles:":K=0
56 H$=B$(K,0):PRINTUSING"[##] SB: ";K+1;:C=INSTR(H$,"\"):A=INSTR(C+1,H$,"\"):PRINTRIGHT$(H$,LEN(H$)-A):PRINTTAB(5)"TO: "LEFT$(H$,C-1):K=K+1:IFK<I THEN56
57 PRINT"*End of Messages*":GOTO12
58 IFF<9THENH$="Z":GOTO15
59 PRINT"PASSWORD?";:GOSUB64:IFH$<>"?DROWSSAP"THENPRINT"Thank You!":GOTO12
60 PRINT"Abort BBS? YES or NO? ";:H=1:GOSUB64:IFH$<>"YES"THEN12
61 GOSUB63:STOP
62 PRINT"%LOAD% [ENTER] WHEN READY";:GOSUB64:OPEN"I",#-1,"USERLOG":INPUT#-1,B,D:FORK=0TOD:INPUT#-1,A$(K):NEXTK:CLOSE:OPEN"I",#-1,"MSG BASE":INPUT#-1,I:FORK=0TOI-1:FORC=0TO10:INPUT#-1,B$(K,C):NEXTC:NEXTK:CLOSE:RETURN
63 PRINT"%SAVE% [ENTER] WHEN READY";:GOSUB64:MOTORON:FORK=0TO999:NEXTK:OPEN"O",#-1,"USERLOG":PRINT#-1,B,D:FORK=0TOD:PRINT#-1,A$(K):NEXTK:CLOSE:OPEN"O",#-1,"MSG BASE":PRINT#-1,I:FORK=0TOI-1:FORC=0TO10:PRINT#-1,B$(K,C):NEXTC:NEXTK:CLOSE:RETURN
64 LINEINPUTH$:H$=LEFT$(H$,64):IFH=0ORH$=""THENRETURN
65 FORA=1TOLEN(H$):J=ASC(MID$(H$,A,1)):IFJ>96THENMID$(H$,A,1)=CHR$(J-32)
66 IFJ=92THENMID$(H$,A,1)="/"
67 NEXTA:H=0:RETURN
68 CLS:PRINTF$A$:PRINTTAB((32-LEN(H$))/2)H$:PRINTA$:RETURN
