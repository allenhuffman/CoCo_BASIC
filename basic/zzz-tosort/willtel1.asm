	NAM	WILLTEL1

TEMPO	EQU	$68
RESTRT	EQU	$FFFE
PORT	EQU	$FF20
SWITCH	EQU	$FF23
	ORG 	$40
MUWORK	RMB	2
DUR	RMB	1
VOICE1	RMB	3
VOICE2	RMB	3
VOICE3	RMB	3
VOICE4	RMB	3
INC1	RMB	2
INC2	RMB	2
INC3	RMB	2
INC4	RMB	2


	ORG	$3F00 *$1000

START	LBRA	MUSIC		GO TO MAIN PRG

*
* INITIALIZATION PROCEDURE
*
INIT	ORCC	#$10
	LDA	#$3F		TURN ON AUDIO
	STA	SWITCH
	LEAX	WFT,PCR		INITIALIZE WAVEFORM POINTERS
	STX	VOICE1
	STX	VOICE2
	STX	VOICE3
	STX	VOICE4
	LEAX	MSTRT,PCR	MUSICSTART
	STX	MUWORK
	RTS			END INIT
*
* PROCEDURE TO READ NEW
* DURATION
*
READ	LDX	MUWORK
	LDA	,X+		GET NEW DURATION
	STA	DUR		STORE IT
	RTS			END READ
*
* PROCEDURE TO GET THE NEXT 
* NOTES FOR THE FOUR VOICES
*

NOTE	LEAY	TABL,PCR	NOTE TABLE
	LDA	,X+		GET NOTE VOICE 1
	LDU	A,Y		GET INCREMENT
	STU	INC1		STORE IT
	LDA	,X+		GET NOTE VOICE 2
	LDU	A,Y		GET INCREMENT
	STU	INC2		STORE IT
	LDA	,X+		GET NOTE VOICE 3
	LDU	A,Y		GET INCREMENT
	STU	INC3		STORE IT
	LDA	,X+		GET NOTE VOICE 4
	LDU	A,Y		GET INCREMENT
	STU	INC4		STORE IT
 	STX	MUWORK		SAVE POINTER
	RTS			END NOTE
*
* PROCEDURE TO PLAY THE MUSIC
*
PLAY	LDY	TEMPO
PLAY1	LDA	[VOICE1]	ADD 4 VOICE SAMPLES
	ADDA	[VOICE2]
	ADCA	[VOICE3]
	ADCA	[VOICE4]

	STA	PORT		TO D/A CONVERTER

	LDD	VOICE1+1	ADD INCREMENTS TO POINTERS FOR THE 4 VOICES
	ADDD	INC1
	STD	VOICE1+1

	LDD	VOICE2+1
	ADDD	INC2
	STD	VOICE2+1

	LDD	VOICE3+1
	ADDD	INC3
	STD	VOICE3+1

	LDD	VOICE4+1
	ADDD	INC4
	STD	VOICE4+1

	LEAY	,-Y		DEC & CHEK TEMPO
	BNE	TIMEWAS		BRA IF NOT OUT
	DEC	DUR		DEC & CHECK DURATION
	BEQ	ENDNOT		JMP IF NOTE END
	BRA	PLAY
TIMEWAS	LDA	,X		PADDING TO MAKE
	BRA	WASTE1
WASTE1	BRA	WASTE2
WASTE2	BRA	WASTE3
WASTE3	BRA	PLAY1		CONTINUE PLAYING
ENDNOT	RTS			END PLAY


*
* MAIN PROGRAM STARTS HERE
*
MUSIC	LBSR	INIT		INITIALIZE
LOOP	BSR	READ		GET DURATION
	CMPA	#0
	BNE OVER
	RTS
	
OVER	BSR	NOTE		GET NOTES
	BSR	PLAY		PLAY IT
	BRA	LOOP		CONTINUE LOOPING

MSTRT	FCB	24,0,0,0,10	; STARTS AT 1217?

*
* WAVEFORM TABLE FOR ORGAN SOUND (256 bytes)
*
WFT	FCB	$0D,$0E,$0F,$11,$12,$14,$15,$16 
	FCB	$18,$19,$1B,$1C,$1D,$1F,$20,$22 
	FCB	$23,$26,$27,$29,$2A,$2B,$2D,$2E 
	FCB	$30,$31,$32,$34,$35,$37,$38,$38 
	FCB	$39,$3B,$3B,$3C,$3C,$3E,$3E,$3E 
	FCB	$3F,$3F,$3F,$3F,$3F,$3F,$3F,$3E 
	FCB	$3E,$3E,$3C,$3C,$3B,$3B,$39,$38 
	FCB	$37,$37,$35,$34,$32,$31,$30,$2E 
	FCB	$2E,$2D,$2B,$2A,$29,$27,$26,$24 
	FCB	$23,$22,$20,$1F,$1F,$1D,$1C,$1B 
	FCB	$1B,$19,$19,$18,$18,$16,$16,$15 
	FCB	$15,$15,$15,$15,$15,$15,$15,$15 
	FCB	$15,$15,$15,$15,$16,$16,$16,$18 
	FCB	$18,$18,$19,$19,$1B,$1B,$1C,$1C 
	FCB	$1C,$1D,$1D,$1F,$1F,$1F,$20,$20 
	FCB	$22,$22,$22,$22,$22,$23,$23,$23 
	FCB	$23,$23,$23,$23,$22,$22,$22,$22 
	FCB	$20,$20,$20,$1F,$1F,$1D,$1D,$1C 
	FCB	$1C,$1B,$19,$19,$18,$16,$15,$15
	FCB	$14,$12,$12,$11,$0F,$0E,$0E,$0D 
	FCB	$0B,$0B,$0A,$08,$08,$07,$07,$06 
	FCB	$06,$04,$04,$03,$03,$03,$01,$01
	FCB	$01,$01,$01,$01,$00,$00,$00,$00
	FCB	$00,$01,$01,$01,$01,$01,$01,$01
	FCB	$03,$03,$03,$03,$03,$04,$04,$04
	FCB	$04,$04,$06,$06,$06,$06,$06,$06 
	FCB	$06,$06,$07,$07,$07,$07,$07,$07 
	FCB	$07,$06,$06,$06,$06,$06,$06,$06 
	FCB	$06,$06,$04,$04,$04,$04,$04,$04 
	FCB	$04,$04,$04,$04,$03,$03,$03,$04 
	FCB	$04,$04,$04,$04,$04,$06,$06,$06 
	FCB	$07,$07,$07,$08,$0A,$0A,$0B,$0D

*
* NOTE TABLE
* A ABOVE MIDDLE C (44O HERTZ)
* IS AT LOCATION $1144
*
TABL	FCB	$00,$00,$02,$6F,$02,$94,$02,$E8 
	FCB	$02,$E4,$03,$10,$03,$3F,$03,$71
	FCB	$03,$A5,$03,$DC,$04,$17,$04,$56 
	FCB	$04,$98,$04,$0D,$05,$28,$05,$76 
	FCB	$05,$C9,$06,$21,$06,$7F,$06,$E2 
	FCB	$07,$4A,$07,$89,$08,$2F,$08,$AC 
	FCB	$09,$30,$09,$BB,$0A,$50,$0A,$ED 
	FCB	$0B,$93,$0C,$43,$0C,$FE,$00,$C4 
	FCB	$0E,$95,$0F,$73				; A ABOVE MIDDLE C (440 HERTZ)?
	FCB	$10,$5F,$11,$58,$12,$60,$13,$77
	FCB	$14,$A0,$15,$DA,$17,$26,$18,$87
	FCB	$19,$FC,$1B,$88,$1D,$2B,$1E,$E7
	FCB	$20,$BE,$22,$B0,$24,$C0,$26,$EF
	FCB	$29,$40,$2B,$B4,$2E,$4D,$31,$0E
	FCB	$33,$F9,$37,$10,$3A,$57,$3D,$CF
	FCB	$41,$7C,$45,$60,$49,$81,$4D,$DF
	FCB	$52,$81,$57,$69,$5C,$9B,$62,$1D
