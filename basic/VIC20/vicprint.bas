0 'VICPRINT.BAS
10 DIM C(1),CA(1),CB(1),CC(1),CD(1),CE(1),CF(1),CG(1),CH(1)
20 DIM CI(1),CJ(1),CK(1),CL(1),CM(1),CN(1),CO(1),CP(1)
40 DIM Z,L,V:GOSUB 2000
50 SX=5*8:EX=SX+21*8:SY=4:ZX=SX:ZY=SY:EY=SY+22*8
60 PMODE 4,1:PCLS1:SCREEN 1,1:LINE(SX-2,SY-2)-(EX+7+2,EY+7+2),PRESET,B
70 Z$="c@ ABCDEFGHIJKL M N O P":GOSUB 1010
80 IF INKEY$="" THEN 80
90 Z$="hdrABCdlllDEFdlllGHIdlllJKL":GOSUB1010
100 Z$="@@@@@@@@@@@@@dl@@@dl@@@dl@@@@@@@@@@@@@@@@@dl@@@dl@@@dl@@@@@@@@@@@@@@@@@dl;":GOSUB1010
110 Z$="@@@dl@@@dl@@@@@@@@@@@@@@@@@@dl@@@dl@@@dl@@@@@@@@@@@ul@ul@ul@":GOSUB1010
115 ZX=SX+15*8:ZY=SY+19*8:GOSUB 1280 'M (LEFT)
116 ZX=SX+19*8:ZY=SY+19*8:GOSUB 1290 'N (RIGHT)
117 ZX=SX+17*8:ZY=SY+19*8:GOSUB 1300 'O (FRONT)
118 ZX=SX+6*8:ZY=SY+4*8:GOSUB 1310 'P
120 IF INKEY$="" THEN 120
130 Z$="c":GOSUB1010
140 Z$="hdrABCdlllDEFdlllGHIdlllJKL":GOSUB1010
150 Z$="@@@@@@@@@@@@dl@@@dl@@@dl@@@dl@@@dl@@@@@@@@@@@@@@@@dl@@@dl@@@dl@@@dl@@@dl;":GOSUB1010
160 Z$="@@@dl@@@dl@@@dl@@@@@@@@@@@@@@@@@@ul@ul@ul@":GOSUB1010
170 IF INKEY$="" THEN 170
180 Z$="c":GOSUB1010
190 Z$="hdrABCdlllDEFdlllGHIdlllJKL":GOSUB1010
200 Z$="@@@@@@@@@dl@@@dl@@@dl@@@uuu@@@dl@@@dl@@@dl@@@@@dl@@@dl@@@dl@@@dl@@@dl@@@dl;":GOSUB1010
210 Z$="@@@uuuul@@@dl@@@@@@@dl@@@dl@@@dl@@@dl@@@dl@@@dl@@@dl@@@@@@@@@ul@ul@ul@ul@ul@":GOSUB1010
220 IF INKEY$="" THEN 220
230 Z$="c":GOSUB1010
240 Z$="@ABBBBBBBBBBBBBBBBBBC@@ DDDDDDDDDDDDDDDDDD @;":GOSUB1010
250 FORA=1TO16:Z$="@                    @;":GOSUB1010:NEXTA
260 Z$="@GHHHHHHHHHHHHHHHHHHI@@@@@@@@@@@@@@@@@@@@@@@  @                @":GOSUB1010
270 Z$=" @@@@@@@@@@@@@@@@@@@@;":GOSUB1010
280 GOTO 280

1000 ' PUT Z$
1010 FOR Z=1 TO LEN(Z$):ZC$=MID$(Z$,Z,1)
1020 IF ZC$=" " THEN 1110
1030 IF ZC$=";" THEN RETURN
1040 IF ZC$="c" THEN PCLS1:ZX=SX:ZY=SY:LINE(SX-2,SY-2)-(EX+7+2,EY+7+2),PRESET,B:GOTO 1120
1050 IF ZC$="h" THEN ZX=SX:ZY=SY:GOTO 1120
1060 IF ZC$="u" THEN ZY=ZY-8:GOTO 1120
1070 IF ZC$="d" THEN ZY=ZY+8:GOTO 1120
1080 IF ZC$="l" THEN ZX=ZX-8:IF ZX<SX THEN ZX=EX:ZY=ZY-8:GOTO 1120 ELSE 1120
1090 IF ZC$="r" THEN ZX=ZX+8:IF ZX>EX THEN ZX=SX:ZY=SY+8:GOTO 1120 ELSE 1120
1100 ON INSTR("-@ABCDEFGHIJKLMNOP",ZC$) GOSUB 1140,1150,1160,1170,1180,1190,1200,1210,1220,1230,1240,1250,1260,1270,1280,1290,1300,1310
1110 ZX=ZX+8:IF ZX>EX THEN ZX=SX:ZY=ZY+8
1120 NEXT
1130 ZX=SX:ZY=ZY+8:RETURN
1140 RETURN
1150 PUT(ZX,ZY)-(ZX+7,ZY+7),C:RETURN
1160 PUT(ZX,ZY)-(ZX+7,ZY+7),CA:RETURN
1170 PUT(ZX,ZY)-(ZX+7,ZY+7),CB:RETURN
1180 PUT(ZX,ZY)-(ZX+7,ZY+7),CC:RETURN
1190 PUT(ZX,ZY)-(ZX+7,ZY+7),CD:RETURN
1200 PUT(ZX,ZY)-(ZX+7,ZY+7),CE:RETURN
1210 PUT(ZX,ZY)-(ZX+7,ZY+7),CF:RETURN
1220 PUT(ZX,ZY)-(ZX+7,ZY+7),CG:RETURN
1230 PUT(ZX,ZY)-(ZX+7,ZY+7),CH:RETURN
1240 PUT(ZX,ZY)-(ZX+7,ZY+7),CI:RETURN
1250 PUT(ZX,ZY)-(ZX+7,ZY+7),CJ:RETURN
1260 PUT(ZX,ZY)-(ZX+7,ZY+7),CK:RETURN
1270 PUT(ZX,ZY)-(ZX+7,ZY+7),CL:RETURN
1280 PUT(ZX,ZY)-(ZX+7,ZY+7),CM:RETURN
1290 PUT(ZX,ZY)-(ZX+7,ZY+7),CN:RETURN
1300 PUT(ZX,ZY)-(ZX+7,ZY+7),CO:RETURN
1310 PUT(ZX,ZY)-(ZX+7,ZY+7),CP:RETURN

2000 ' LOAD FONT CHARACTERS
2010 PRINT "LOADING DATA";
2020 L=VARPTR(C(0)):GOSUB 3000
2030 L=VARPTR(CA(0)):GOSUB 3000
2040 L=VARPTR(CB(0)):GOSUB 3000
2050 L=VARPTR(CC(0)):GOSUB 3000
2060 L=VARPTR(CD(0)):GOSUB 3000
2070 L=VARPTR(CE(0)):GOSUB 3000
2080 L=VARPTR(CF(0)):GOSUB 3000
2090 L=VARPTR(CG(0)):GOSUB 3000
2100 L=VARPTR(CH(0)):GOSUB 3000
2110 L=VARPTR(CI(0)):GOSUB 3000
2120 L=VARPTR(CJ(0)):GOSUB 3000
2130 L=VARPTR(CK(0)):GOSUB 3000
2140 L=VARPTR(CL(0)):GOSUB 3000
2150 L=VARPTR(CM(0)):GOSUB 3000
2160 L=VARPTR(CN(0)):GOSUB 3000
2170 L=VARPTR(CO(0)):GOSUB 3000
2180 L=VARPTR(CP(0)):GOSUB 3000
2190 RETURN

3000 ' READ DATA AND POKE AT L
3010 PRINT ".";
3020 FOR Z=L TO L+7:READ V:POKE Z,(NOT V)+256:NEXT:RETURN
3030 ' SKY-APE-ER CHARS
3040 ' @ - BRICK PLATFORM
3050 DATA 223,223,223,0,253,253,253,0
3060 ' A - APE ROW 1 LEFT
3070 DATA 0,0,0,0,1,3,3,7
3080 ' B - APE ROW 1 MIDDLE
3090 DATA 0,60,126,219,129,165,165,129
3100 ' C - APE ROW 1 RIGHT
3110 DATA 0,0,0,0,128,192,192,224
3120 ' D - APE ROW 2 LEFT
3130 DATA 31,63,127,255,252,254,127,63
3140 ' E - APE ROW 2 MIDDLE
3150 DATA 0,126,126,0,255,0,231,231
3160 ' F - APE ROW 2 RIGHT
3170 DATA 248,252,254,255,63,127,254,252
3180 ' G - APE ROW 3 LEFT
3190 DATA 15,7,8,30,31,31,31,31
3200 ' H - APE ROW 3 MIDDLE
3210 DATA 231,219,60,255,126,126,189,195
3220 ' I - APE ROW 3 RIGHT
3230 DATA 240,224,16,120,248,248,248,248
3240 ' J - APE ROW 4 LET
3250 DATA 31,31,31,15,15,63,127,0
3260 ' K - APE ROW 4 MIDDLE
3270 DATA 231,129,0,0,129,129,129,0
3280 ' L - APE ROW 4 RIGHT
3290 DATA 248,248,248,240,240,252,254,0
3300 ' M - MAN RIGHT
3310 DATA 12,12,24,47,8,15,82,36
3320 ' N - MAN LEFT
3330 DATA 48,48,24,244,16,240,74,36
3340 ' O - MAN FRONT
3350 DATA 28,8,28,42,8,20,20,54
3360 ' P - CHIMP
3370 DATA 12,76,40,47,40,159,82,36
3380 DATA -1
