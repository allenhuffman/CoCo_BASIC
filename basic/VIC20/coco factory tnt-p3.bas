5 REM***************                 * FACTORY TNT *                 ******BY:******
6 REM*ALLEN HUFFMAN*                 ***************
10 PMODE 4,1:PCLS1:SCREEN 1,1:ML=3:SC=0
15 LINE(0,8*1)-(7,8*20),PRESET,BF:LINE(8*21,8*1)-(8*21+7,8*20),PRESET,BF
16 LINE(0,8*20)-(8*21+7,8*20+7),PRESET,BF:LINE(8,8*22)-(8*20+7,8*22+7),PRESET,BF
17 LINE(8*2,8*21)-(8*2+7,8*21+7),PRESET,BF:LINE(8*19,8*21)-(8*19+7,8*21+7),PRESET,BF
35 L=&HE00+(32*8*18)+10
40 B=RND(18)+&HF02
42 REM PRINT"{home}{space*2}{reverse on}men:{reverse off}";ML;" {reverse on}score:{reverse off}";SC
45 GOSUB1110:GOSUB1010
50 A=A+1:IFA=>3THENGOSUB1060:B=B+&H100:GOSUB1010:A=1
55 IFB=>&H2000 THEN150
95 POKE&H153,&HFF:POKE&H155,&HFF
100 A$=INKEY$:IFA$=""THEN45
105 GOSUB1160
110 IFA$="A"THENL=L-1
115 IFA$="S"THENL=L+1
120 IFPEEK(L)=0THEN130
125 GOTO45
130 IFA$="A"THENL=L+1
135 IFA$="S"THENL=L-1
140 GOTO45
150 IFB=L THEN200
155 GOSUB1210
160 SOUND1,5
170 GOSUB1060
175 ML=ML-1
180 IFML=0THEN250
190 GOTO40
199 REM 117, 185
200 SC=SC+10:SOUND150,1:SOUND200,1
205 GOTO40
250 CLS
255 PRINT"**********************":PRINT"*******game over******":PRINT"**********************":PRINT
260 IF SC>TP THEN TP=SC:GOTO300
265 PRINT"final score:";SC
270 PRINT:PRINT"top score  :";TP
275 PRINT:PRINT"====player rating===="
285 IFSC=<50THENR$="dishwasher"
286 IFSC<100ANDSC>50THENR$="private"
287 IFSC=>100ANDSC<200THENR$="getting better"
288 IF SC=>200ANDSC<300THENR$="well done!"
289 IFSC=>300THENR$="super fantastic, extra"+CHR$(13)+"great, top dog, #1"
290 PRINT:PRINT"rank:":PRINT:PRINTR$
295 PRINT:PRINT"will you play again?";
296 Q$=INKEY$:IFQ$=""THEN296
297 IFQ$="N"THEN350
298 IFQ$="Y"THEN10
299 GOTO296
300 PRINT"**********************":PRINT"******top score*******":PRINT"**********************":PRINT
302 RESTORE
305 READA:IFA=-1THEN250
306 IFA=0THENFORA=1TO90:NEXT:GOTO305
310 SOUNDA,2
325 GOTO305
350 REM DATA135,147,159,0,135,147,159,0,135,147,159,0,163,175,0,0,135,147,159,0,135,147,159
351 DATA     89,108,125,0, 89,108,125,0, 89,108,125,0,133,147,0,0, 89,108,125,0, 89,108,125
355 REM DATA0,135,147,159,0,175,147,-1
356 DATA 0, 89,108,125,0,147,108,-1


400 END

1000 REM BOMB AT B
1010 POKEB,&HE7:POKEB+&H20,&HE7:POKEB+&H40,&HC3:POKEB+&H60,&H89:POKEB+&H80,&H85:POKEB+&HA0,&H81:POKEB+&HC0,&H81:POKEB+&HE0,&HC3:RETURN
1050 REM ERASE AT B
1060 POKEB,&HFF:POKEB+&H20,&HFF:POKEB+&H40,&HFF:POKEB+&H60,&HFF:POKEB+&H80,&HFF:POKEB+&HA0,&HFF:POKEB+&HC0,&HFF:POKEB+&HE0,&HFF:RETURN

1100 REM PLAYER AT L
1110 POKEL,&HFF:POKEL+&H20,&HFF:POKEL+&H40,.:POKEL+&H60,.:POKEL+&H80,&H7E:POKEL+&HA0,&H7E:POKEL+&HC0,&HBD:POKEL+&HE0,&HC3:RETURN
1150 REM ERASE AT L
1160 POKEL,&HFF:POKEL+&H20,&HFF:POKEL+&H40,&HFF:POKEL+&H60,&HFF:POKEL+&H80,&HFF:POKEL+&HA0,&HFF:POKEL+&HC0,&HFF:POKEL+&HE0,&HFF:RETURN

1200 REM MUSHROOM AT B
1210 POKEB,&H81:POKEB+&H20,.:POKEB+&H40,.:POKEB+&H60,.:POKEB+&H80,&HA5:POKEB+&HA0,&HE7:POKEB+&HC0,&HE7:POKEB+&HE0,&HC3:RETURN
